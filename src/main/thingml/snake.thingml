/**
File: snake.thingml

Result: Snake game

HardWare required: 	Arduino Uno compatible board
					an adafru.it shield LCD 1.8" with joystick
					
Feature taken up: 	- lib
					- an example of poor not well thought out data structure for arduino in snakeList/_snakeList.thingml
					- an example of listner on button's change
					
TODO: compile and run 

TODO: your own app
*/

import "lib/_1_8pLCD.thingml"
import "snakeList/_snakeList.thingml"

configuration SnakeCfg 

//@debug "true"
//@debug_fifo "true" //this options, once both uncommented will print on the serial port when the fifo for messages overflows
{
    
    instance myList : snakeList
	instance myLCD : Adafruit_1_8pLCDShieldShield
    instance arduinoScheduler:  ArduinoScheduler
    instance application: ArduinoSketch	
	connector myList.lcd => myLCD.lcd
	connector myList.arduino => arduinoScheduler.arduino
	connector application.lcd => myLCD.lcd
	connector application.list => myList.list
	connector application.button => myLCD.button
	connector application.arduino => arduinoScheduler.arduino
	connector myLCD.arduino => arduinoScheduler.arduino
}

thing ArduinoSketch includes Adafruit_1_8pLCDShieldClient, snakeListClient
{
	
	property headx : UInt8
	property heady : UInt8
	property nextmovex : UInt8
	property nextmovey : UInt8
	property applex : UInt8
	property appley : UInt8
	property timer : UInt8  = 1
	property speed : Integer = 200
	property direction : UInt8 = 2
	property lost : Boolean = false
	property score : UInt8 = 0
	
	function newApple() do
		applex = '(uint8_t) random(0, 42)'
		appley = '(uint8_t) random(0, 52)'
		lcd!drawRect('2+3*'&applex,'2+3*'&appley,2,2,'0xF800')
	end
	
	statechart ArduinoSketchStateChart init Setup {
		state Setup {
			transition -> Menu event lcd?LCDready
		}
		state Menu {
			on entry do
				arduino!serial_print_str("Snake Init\n")
				lcd!clear()
                lcd!drawRect(1,1,127,157,'0xFFFF')
                lcd!set_cursor(16,68)
                lcd!print_str("Welcome to Snake!")
                lcd!set_cursor(12,78)
                lcd!print_str("Press the joystick")
                lcd!set_cursor(40,88)
                lcd!print_str("to start")
			end
			transition -> Game event e : button?button_state_change
		}

		composite state Game init Init {
			state Init {
				on entry do
					arduino!serial_print_str("Snake Start\n")
					lcd!clear()
	                lcd!drawRect(1,1,127,157,'0xFFFF')
					direction = 2
					lost = false
					score = 0
					headx = 21
					heady = 26
					list!flush()
					list!addHead(21,26)
					list!refreshPrintList()
					//newApple()
					arduino!timer_start(timer, speed)
				end
				//transition -> Control event e : arduino?timeout
				transition -> NewApple event e : arduino?timeout
			}
			
			state Control {
				on entry do
					if(direction == 1) do
						nextmovex = headx
						nextmovey = heady + 1
						heady = heady + 1
					end
					if(direction == 2) do
						nextmovex = headx + 1
						nextmovey = heady
						headx = headx + 1
					end
					if(direction == 4) do
						nextmovex = headx
						nextmovey = heady - 1
						heady = heady - 1
					end
					if(direction == 5) do
						nextmovex = headx - 1
						nextmovey = heady
						headx = headx - 1
					end
					if ((nextmovex > 41) or (nextmovey > 51)) do
						lost = true
						arduino!serial_print_str("[Snake] met a wall\n")
					end
					list!isInSnake(nextmovex, nextmovey)
				end
				
				internal event e : button?button_state_change action do
					if((e.bstate == 1) or (e.bstate == 2) or (e.bstate == 4) or (e.bstate == 5)) do
						direction = e.bstate
					end
				end
				
				transition -> Move event e : list?isInSnakeResponse guard (not(lost) and not(e.b))
				transition -> End event e : list?isInSnakeResponse guard (lost or e.b)
				
			}
			
			state Move {
				on entry do
					list!addHead(nextmovex,nextmovey)
					/*
					if (not(nextmovex == applex) or not(nextmovey == appley)) do
						list!dropTail()
					end
					if ((nextmovex == applex) and (nextmovey == appley)) do
						score = score + 1
						newApple()
					end*/
					
					arduino!timer_start(timer, speed)
				end
				
				internal event e : button?button_state_change action do
					if((e.bstate == 1) or (e.bstate == 2) or (e.bstate == 4) or (e.bstate == 5)) do
						direction = e.bstate
					end
				end
				
				transition -> NewApple event e : arduino?timeout guard ((nextmovex == applex) and (nextmovey == appley)) action do
						score = score + 1
					list!refreshPrintList()
				end
				
				transition -> Control event e : arduino?timeout guard (not(nextmovex == applex) or not(nextmovey == appley)) action do
					list!dropTail()
					list!refreshPrintList()
				end
			}
		
			state NewApple {
				on entry do
					applex = '(uint8_t) random(0, 42)'
					appley = '(uint8_t) random(0, 52)'
					list!isInSnake(applex, appley)
				end
				
				transition -> NewApple event e : list?isInSnakeResponse guard e.b
				transition -> Control event e : list?isInSnakeResponse guard not(e.b) action do
					lcd!drawRect('2+3*'&applex,'2+3*'&appley,2,2,'0xF800')
				end
			}
		
			state End {
				on entry do
					arduino!serial_print_str("Snake Ended\n")
					lcd!clear()
	                lcd!drawRect(1,1,127,157,'0xFFFF')
	                lcd!set_cursor(16,68)
	                lcd!print_str("You scored: ")
	                lcd!print_num(score)
	                lcd!set_cursor(12,78)
	                lcd!print_str("Press the joystick")
	                lcd!set_cursor(22,88)
	                lcd!print_str("to start again")
				end
			transition -> Init event e : button?button_state_change
				
			}
		}
	}
}